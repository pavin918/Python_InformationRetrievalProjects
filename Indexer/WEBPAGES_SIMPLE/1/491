 <body>

<h1>
  VHDL MINI-REFERENCE</h1>

 
See the VHDL Language Reference Manual (<b>VLRM</b>) for Additional Details 
The following Mini-Reference can be divided into the following parts:
 I.       Primary Design Unit
Model Structure 
         A.     Entity
Declaration Format 
         B.     Architecture 
 II.     Packages 
         A.     Declaration
and Libraries 
         B.     Identifiers,
Numbers, Strings, and Expressions 
         C.     Data
Types 
         D.     Objects:
Signals, Constants, and Variables 
         E.     Concurrent
Statements 
                
1)         Signal
Assignment 
                
2)         Process
Statement 
                
3)         Block Statement 
                
4)         Procedure
Statement 
                
5)         Component
Instantiation 
                
6)         Concurrent
Assertion 
                
7)         Generate
Statement 
         F.     Sequential
Statements 
                
1)         Wait Statement 
                
2)         Signal
Assignment 
                
3)         Variable
Assignment 
                
4)         Procedure
Call 
                
5)         Conditional
Statements 
                
6)         Loop Statements 
                
7)         Procedure
Statement 
                
8)         Function
Statement 
         G.     Other
IEEE "std.logic" Functions 
         H.     Object
Attributes 
         I.    
 The TEXTIO Package 
 <h1>
  PRIMARY DESIGN UNIT MODEL STRUCTURE -  Back
To Top </h1>
Each VHDL design unit comprises an "entity" declaration and one or more
"architectures". Each architecture defines a different implementation or
model of a given design unit. The entity definition defines the inputs
to, and outputs from the module, and any "generic" parameters used by the
different implementations of the module.
<h2>
  Entity Declaration Format -  Back To
Top </h2>

     <b>entity</b>  name  <b>is
</b>        <b>port(</b> port definition list <b>);</b>-- input/output signal ports
        <b>generic(</b> generic list<b>);</b>   -- optional generic list
    <b>end</b> name; 
Port declaration format:  port_name: mode data_type; 
 The  mode  of a port defines the directions of the singals on that
pirt, and is one of: <b>in</b>, <b>out</b>, <b>buffer</b>, or
<b>inout</b>.
  Port Modes:
 
An <b>in</b> port 

 
can be read but not updated within the module, carrying information into
the module. (An in port cannot appear on the left hand side of a signal
assignment.) 

 
An <b>out</b> port 

 
can be updated but not read within the module, carrying information out
of the module. (An out port cannot appear on the right hand side of a signal
assigment.) 

 
A <b>buffer</b> port 

 
likewise carries information out of a module, but can be both updated and
read within the module. 

 
An <b>inout</b> port 

 
is bidirectional and can be both read and updated, with multiple update
sources possible. 
  
NOTE: A buffer is strictly an output port, i.e. can only be driven from
within the module, while inout is truly bidirectional with drivers both
within and external to the module. 

 
Example 

    entity counter is
        port (Incr, Load, Clock: in     bit;
              Carry:             out    bit;
              Data_Out:          buffer bit_vector(7 downto 0);
              Data_In:           in     bit_vector(7 downto 0));
   end counter; 
<b>Generics</b> allow static information to be communicated to a block
from its environment for all architectures of a design unit. These include
timing information (setup, hold, delay times), part sizes, and other parameters.
 
<b>Example</b> 

     entity and_gate is
        port(a,b: in  bit;
             c:   out bit);
        generic (gate_delay: time := 5ns);
    end and_gate; 

<h2>
  Architecture -  Back To Top </h2>
An architecture defines one particular implementation of a design unit,
at some desired level of abstraction.
   <b>architecture</b> arch_name <b>of</b> entity_name <b>is
</b>       ...  declarations ...
   begin
       ...  concurrent statements  ...
   end 
 Declarations  include data types, constants, signals, files, components,
attributes, subprograms, and other information to be used in the implementation
description.  Concurrent statements  describe a design unit at one
or more levels of modeling abstraction, including dataflow, structure,
and/or behavior.
  
<b>Behavioral Model:</b> No structure or technology implied. Usually written
in sequential, procedural style. 

 
<b>Dataflow Model:</b> All datapaths shown, plus all control signals. 

 
<b>Structural Model:</b> Interconnection of components. 
 <h1>
  VHDL PACKAGES -  Back To Top </h1>
A VHDL  package  contains subprograms, constant definitions, and/or
type definitions to be used throughout one or more design units. Each package
comprises a "declaration section", in which the available (i.e. exportable)
subprograms, constants, and types are declared, and a "package body", in
which the subprogram implementations are defined, along with any internally-used
constants and types. The declaration section represents the portion of
the package that is "visible" to the user of that package. The actual implementations
of subroutines in the package are typically not of interest to the users
of those subroutines.
<h3>
Package declaration format:</h3>

    <b>package</b> package_name <b>is
</b>     ... exported constant declarations
     ... exported type declarations
     ... exported subprogram declarations
   <b>end</b> package_name; 

 
Example: 

     package ee530 is
       constant maxint: integer := 16#ffff#;
       type arith_mode_type is (signed, unsigned);
       function minimum(constant a,b: in integer) return integer;
    end ee530; 

<h3>
Package body format:</h3>

    <b>package body</b> package_name <b>is
</b>       ... exported subprogram bodies
       ... other internally-used declarations
   <b>end</b> package_name; 

 
Example: 

    package body ee530 is
      function minimum (constant a,b: integer) return integer is
         variable c: integer; -- local variable
             begin
                if a &lt; b then
                    c := a;  -- a is min
                else
                    c := b;  -- b is min
                end if;
                return c;  -- return min value
             end;
    end ee530; 

<h3>
Package Visibility</h3>
To make all items of a package "visible" to a design unit, precede the
desired design unit with a "use" statement:
 
Example: 

    <b>use</b> library_name.package_name.all 
A "use" statement may precede the declaration of any entity or architecture
which is to utilize items from the package. If the "use" statement precedes
the entity declaration, the package is also visible to the architecture.
<h3>
User-Developed Packages</h3>
Compile user-developed packages in your current working library. To make
it visible:
     <b>use</b> package_name<b>.all;</b> 
Note: '<b>std</b>' and '<b>work</b>' (your current working library) are
the two default libraries. The VHDL 'library' statement is needed to make
the 'ieee' library and/or additional libraries visible.
 
Example 

    <b>library</b> lib_name;            -- make library visible
   <b>use</b> lib_name.pkg_name.all;   -- make package visible 

<h3>
VHDL Standard Packages</h3>

 STANDARD - basic type declarations (always visible by default)
 TEXTIO - ASCII input/output data types and subprograms 
To make TEXTIO visible: <b>use</b> std.textio.all;
<h3>
IEEE Standard 1164 Package</h3>
This package contained in the 'ieee' library supports multi-valued logic
signals with type declarations and functions. To make visible:
    <b>library ieee;</b>      -- VHDL Library stmt
      <b>use</b> <b>ieee.</b>std_logic_1164<b>.all;</b> 

<h3>
Special 12-valued data types/functions to interface with QuickSim II and
schematic diagrams.</h3>

    library mgc_portable;            -- Special Mentor Graphics Library
   use mgc_portable.qsim_logic.all; -- Quicksim portable data types 

<h2>
  VHDL IDENTIFIERS, NUMBERS, STRINGS, AND EXPRESSIONS
-  Back To Top </h2>

<h3>
Identifiers</h3>
Identifiers in VHDL must begin with a letter, and may comprise any combination
of letters, digits, and underscores. Note that VHDL internally converts
all characters to UPPER CASE.
 
Examples 

      Memory1, Adder_Module, Bus_16_Bit 

<h3>
Numeric Constants</h3>
Numeric contants can be defined, and can be of any base (default is decimal).
Numbers may include embedded underscores to improve readability.
 Format: <b>base#digits#</b> -- base must be a decimal number
  
Examples 

      16#9fba#           (hexadecimal)
     2#1111_1101_1011#  (binary)
     16#f.1f#E+2        (floating-point, exponent is decimal)

 

<h3>
Bit String Literals</h3>
Bit vector constants are are specified as literal strings.
 
Examples 

      x"ffe"            (12-bit hexadecimal value)
     o"777"            (9-bit octal value)
     b"1111_1101_1101" (12-bit binary value) 

<h3>
Arithmetic and Logical Expressions</h3>
Expressions in VHDL are similar to those of most high-level languages.
Data elements must be of the type, or subtypes of the same base type. Operators
include the following:
  
Logical: <b>and</b>, <b>or</b>, <b>nand</b>, <b>nor</b>,
<b>xor</b>, <b>not</b>
(for boolean or bit ops) 

 
Relational: =, /=, &lt;, &lt;=, &gt;, &gt;= 

 
Arithmetic: +, -, *, /, mod, rem, **, abs 

 (a mod b takes sign of b, a rem b takes sign of a)
 
Concatenate: <b>&amp;</b> 

 (ex. a &amp; b makes one array)  
Examples 

    a &lt;= b nand c;
   d := g1 * g2 / 3;
   Bus_16 &lt;= Bus1_8 &amp; Bus2_8; 

<h2>
  VHDL DATA TYPES -  Back To Top </h2>
Each VHDL objects must be classified as being of a specific data type.
VHDL includes a number of predefined data types, and allows users to define
custom data types as needed.
<h3>
Predefined Scalar Data Types (single objects)</h3>

 
VHDL Standard: 

  
<b>bit</b> values: '0', '1' 

 
<b>boolean</b> values: TRUE, FALSE 

 
<b>integer</b> values: -(231) to +(231 - 1) {SUN Limit} 

 
<b>natural</b> values: 0 to integer'high (subtype of integer) 

 
<b>positive</b> values: 1 to integer'high (subtype of integer) 

 
<b>character</b> values: ASCII characters (eg. 'A') 

 
<b>time</b> values include units (eg. 10ns, 20us) 
  
IEEE Standard 1164 (package ieee.std_logic_1164.all) 

  
<b>std_ulogic</b> values: 'U','X','1','0','Z','W','H','L','-' 

 'U' = uninitialized
 'X' = unknown
 'W' = weak 'X'
 'Z' = floating
 'H'/'L' = weak '1'/'0'
 '-' = don't care  
<b>std_logic</b> resolved "std_ulogic" values 

 
<b>X01</b> subtype {'X','0','1'} of std_ulogic 

 
<b>X01Z</b> subtype {'X','0','1','Z'} of std_ulogic 

 
<b>UX01</b> subtype {'U','X','0','1'} of std_ulogic 

 
<b>UX01Z</b> subtype {'U','X','0','1','Z'} of std_ulogic 
 <h3>
Predefined VHDL Aggregate Data Types</h3>

  
<b>bit_vector</b> array (natural range &lt;&gt;) of bit 

 
<b>string</b> array (natural range &lt;&gt;) of char 

 
<b>text</b> file of "string" 
 <h3>
IEEE Standard 1164 Aggregate Data Types</h3>
(From package:  ieee.std_logic_1164.all )
  
<b>std_ulogic_vector</b> array (natural range &lt;&gt;) of std_ulogic 

 
<b>std_logic_vector</b> array (natural range &lt;&gt;) of std_logic 
  
<b>Examples</b> 

     signal dbus: bit_vector(15 downto 0);
    dbus (7 downto 4) &lt;= "0000"; (4-bit slice of dbus)
    signal cnt:  std_ulogic_vector(1 to 3);
    variable message: string(0 to 20); 

<h3>
User-Defined Enumeration Types</h3>
An enumerated data type can be created by explicitely listing all possible
values.
 
Example 

    type opcodes is (add, sub, jump, call);  -- Type with 4 values
   signal instruc: opcodes;                 -- Signal of this type
     ...
   
   if instruc = add then   -- test for value 'add'
     ... 

<h3>
Other user-defined types</h3>
Custom data types can include arrays, constrained and unconstrained, and
record structures.
  
 Constrained array:  Upper and lower indexes are specified. 

 
Example 

     <b>type</b> word <b>is</b> <b>array</b> (0 to 15) <b>of</b> bit; 

 
 Unconstrained array:  Indexes are specified when a signal or variable
of that type is declared. 

 
Examples 

    <b>type</b> memory <b>is</b> array (integer range &lt;&gt;) of bit_vector(0 to 7);
  -- a type which is an arbitrary-sized array of 8-bit vectors
   <b>variable</b> memory256: memory(0 to 255); -- a 256-byte memory array
   <b>variable</b> stack: memory(15 downto 0);  -- a 16-byte memory array 

 
 Subtype:  A selected subset of values of a given type. Elements of
different subtypes having the same base type may be combined in expressions
(elements of different types cannot). Subtypes can be used to detect out-of-range
values during simulation. 

 
Examples 

    <b>subtype</b> byte_signed <b>is</b> integer <b>range</b> -128 to 127;
   <b>subtype</b> byte_unsigned <b>is</b> integer <b>range</b> 0 to 255; 


<h3>
Aliases</h3>
An alias" defines an alternate name for a signal or part of a signal. Aliases
are often used to refer to selected slices of a bit_vector.
 
Example 

    signal instruction: bit_vector(31 downto 0);
   alias opcode: bit_vector(6 downto 0) is instruction(31 downto 25);
    ...
   opcode &lt;= "1010101";  -- Set the opcode part of an instruction code 
 
<h2>
  VHDL OBJECTS: CONSTANTS, VARIABLES, AND SIGNALS -
 Back
To Top </h2>

<h3>
Constants</h3>
A  constant  associates a value to a symbol of a given data type.
The use of constants may improve the readability of VHDL code and reduce
the likelihood of making errors. The declaration syntax is:
 <b>constant</b> symbol: type := value;  
Examples 

   <b>constant</b>  Vcc:  signal:= '1';   --logic 1 constant
  <b>constant</b>  zero4: bit_vector(0 to 3) := ('0','0','0','0'); 

<h3>
Variables</h3>
A  variable  is declared within a blocks, process, procedure, or function,
and is updated immediately when an assignment statement is executed. A
variable can be of any scalar or aggregate data type, and is utilized primarily
in behavioral descriptions. It can optionally be assigned initial values
(done only once prior to simulation). The declaration syntax is:
 <b>variable</b> symbol: type [:= initial_value];  
Examples 

    process
       variable count: integer  := 0;
       variable rega: bit_vector(7 downto 0);
   begin
       ...
       count := 7;      -- assign values to variables
       rega  := x"01";
       ...
   end; 

<h3>
Signals</h3>
A  signal  is an object with a history of values (related to "event"
times, i.e. times at which the signal value changes).
 Signals are declared via signal declaration statements or entity port
definitions, and may be of any data type. The declaration syntax is:
  <b>signal</b> sig_name: data_type [:=initial_value];  
Examples 

     signal clock: bit;
    signal GND:   bit := '0';
    signal databus: std_ulogic_vector(15 downto 0);
    signal addrbus: std_logic_vector(0 to 31); 
Each signal has one or more "drivers" which determine the value and timing
of changes to the signal. Each driver is a queue of events which indicate
when and to what value a signal is to be changed. Each signal assignment
results in the corresponding event queue being modified to schedule the
new event.
 
 
signal line x 

  10ns '0' Driver of
  20ns '1' signal x
  
Event Values 

 
Times 
 
NOTE: If no delay is specified, the signal event is scheduled for one infinitessimally-small
"delta" delay from the current time. The signal change will occur in the
next simulation cycle.
 
Examples 
(Assume current time is T)
     clock   &lt;= not clock after 10ns;      -- change at T + 10ns
    databus &lt;= mem1 and mem2 after delay; -- change at T + delay
    x       &lt;= '1';                       -- change to '1' at time T + "delta"; 
Element delay models may be specified as either "inertial" or "transport".
Inertial delay is the default, and should be used in most cases.
  
<b>Inertial delay:</b> The addition to an event queue of an event scheduled
at time T automatically cancels any events in the queue scheduled to occur
prior to time T, i.e. any event shorter than the delay time is suppressed. 

 
<b>Transport delay</b>: Each new event is simply inserted into the event
queue, i.e. behavior is that of a delay line. The keyword <b>transport</b>
is used to indicate transport delays. 
  
Examples 

     B &lt;= A after 5ns;            -- inertial delay
    C &lt;= transport A after 5 ns; -- transport delay 

              5______15 17_________30
    A _______|       |_|          |_____________ 
                  ____________________
    B ___________|                    |_________ (Inertial Delay)
                  _______   __________
    C ___________|       |_|          |_________ (Transport Delay)
                10      20 22         35 
Where there are multiple drivers for one signal, a "resolution function"
must be provided to determine the value to be assigned to the signal from
the values supplied by the multiple drivers. This allows simulation of
buses with multiple sources/drivers.
 NOTE: The  std_logic  and  std_logic_vector  types from the
ieee library have predefined resolution functions:
  
Example 

     signal data_line: std_logic;
    begin
      block1:  
          data_line &lt;= '1';     -- one driver
          ...
      block2:
          data_line &lt;= 'Z';  -- 2nd driver 
The resolved value is '1' since '1' overrides a 'Z' (floating) value. If
the two values had been '1' and '0', the resolved value would have been
'X', indicating an unknown result.
<h2>
  CONCURRENT STATEMENTS -  Back To
Top </h2>
Concurrent statements are included within architecture definitions and
within "block" statements, representing concurrent behavior within the
modelled design unit. These statements are executed in an asynchronous
manner, with no defined order, modeling the behavior of independent hardware
elements within a system.
<h3>
Concurrent Signal Assignment</h3>
A signal assignment statement represents a process that assigns values
to signals. It has three basic formats.
  
A &lt;= B; A &lt;= B when condition1 elseC <b>when</b> condition2
<b>else</b>
D <b>when</b> condition3 <b>else</b> E; 

 
<b>with</b> expression <b>select</b> A <b>&lt;=</b> B <b>when</b> choice1,
C <b>when</b> choice2, D <b>when</b> choice3, E when others; 
 
For each of the above, waveforms (time-value pairs) can also be specified.
 
Examples 

     A &lt;= B after 10ns when condition1 else
         C after 12ns when condition2 else
         D after 11ns;

    -- 4-input multiplexer (Choice is a 2-bit vector)
    with Choice select 
         Out &lt;=  In0 after 2ns when "00",
                 In1 after 2ns when "01",
                 In2 after 2ns when "10",
                 In3 after 2ns when "11";

    -- 2-to-4 decoder (Y = 4-bit and A = 2-bit vectors)
    Y &lt;= "0001" after 2ns when A = "00" else
         "0010" after 2ns when A = "01" else
         "0100" after 2ns when A = "10" else
         "1000" after 2ns ;

    -- Tri-state driver: (Y is logic4; X is bit_vector)
    Y &lt;= '0' after 1ns when En = '1' and X = '0' else
         '1' after 1ns when En = '1' and X = '1' else
         'Z' after 1ns;

    -- A is a 16-bit vector
    A &lt;= (others =&gt; '0');   -- set all bits of A to '0' 
The keyword "others" in the last example indicates that all elements of
A not explicitly listed are to be set to '0'.
<h3>
  Process Statement -  Back To Top </h3>
An independent sequential process represents the behavior of some portion
of a design. The body of a process is a list of sequential statements.
 
Syntax: 

     label: <b>process</b> (sensitivity list)   
           ... local declarations ...    
           <b>begin
</b>           ... sequential statements ...
           <b>end process</b> label; 

 
Example 

    DFF: process (clock)
          begin
             if clock = '1' then
                Q  &lt;= D after 5ns;
                QN &lt;= not D after 5ns;
             end if;
        end process DFF; 
The sequential statements in the process are executed in order, commencing
with the beginning of simulation. After the last statement of a process
has been executed, the process is repeated from the first statement, and
continues to repeat until suspended. If the optional sensitivity list is
given, a <b>wait on</b> ... statement is inserted after the last sequential
statement, causing the process to be suspended at that point until there
is an event on one of the signals in the list, at which time processing
resumes with the first statement in the process.
<h3>
  Block Statement -  Back To Top </h3>
A  block  is a grouping of related concurrent statements that can
be used in representing designs in a hierarchical manner.
 
Syntax: 

    label: <b>block</b> (guard expression)
          ... local declarations ...
          begin
          ... concurrent statements ...
          end block label; 
If a  guard expression  is given, "guarded" a boolean variable GUARD
is automatically defined and set to the boolean value of the guard expression.
GUARD can then be tested within the block, to perform selected signal assignments
or other statements only when the guard condition evaluates to TRUE.
 
Examples 

     -- D Latch: Transfer D input to Q output when Enable = '1' 
    block (Enable = '1')
    begin
       Q &lt;= guarded D after 5ns;

    end block;

    -- D Flip-flop: Transfer D to Q on falling edge of Clock
    block (Clock'EVENT and Clock = '0')
    begin
       Q &lt;= guarded D after 5ns;
    end block;

    -- Tristate driver with input B and output A 
    block (Enable = '1')
    begin
        A &lt;= B when GUARD = '1' else 'Z';
    end block;

 
In the last example, B is assigned to signal A only when GUARD is true,
which implies Enable = '1'.
<h3>
  Concurrent Procedure Call -  Back
To Top </h3>
An externally defined procedure/subroutine can be invoked, with parameters
passed to it as necessary. This serves the same function and behaves in
the same manner as a "process" statement, with any signals in the passed
parameters forming a sensitivity list.
 
Example 

    ReadMemory (DataIn, DataOut, RW, Clk);
   (where the ReadMemory procedure is defined elsewhere) 

<h3>
  Component instantiation -  Back
To Top </h3>
Instantiates (i.e. create instances of) predefined components within a
design architecture. Each such component is first declared in the declaration
section of that architecture, and then "instantiated" one or more times
in the body of the architecture.
  
In the declaration section: list the "component declaration" and one or
more "configuration specifications".   

 The "component declaration" defines the component interface, which
corresponds to the component's entity declaration. This allows the VHDL
compiler to check signal compatibilities.
 
Example 

      component adder
       port(a,b:  in  bit_vector(7 downto 0);
            s:    out bit_vector(7 downto 0);
            cin:  in  bit;
            cout: out bit);
    end component; 

 
The "configuration specification" identifies specific architecture(s) to
be used for each instance of the component. (There may be multiple architectures
for a given component.) 

 
Examples 

     for ALL:     comp1 use entity work.comp1 (equations);
    for ADDER1:  adder use entity work.adder (equations);
    for ADDER2:  adder use entity work.adder (dataflow); 
In all three examples, the prefix <b>work.</b> indicates that the current
working library contains the indicated component models. In the first example,
architecture  equations  of entity
 comp1  is used for all instances
of  comp1 . In the other examples, architecture  equations  is
to be used for instance  ADDER1  of component  adder , and architecture
 dataflow 
is to be used for instance ADDER2 of component  adder .

<h3>
Component Instantiation Each instance of a declared component is listed,
an instance name assigned, and actual signals connected to its ports as
follows:</h3>

 instance_name: component_name <b>port map</b> (port list); 
The port list may be in either of two formats:
 
(1) "Positional association": signals are connected to ports in the order
listed in the component declaration. 

 
Ex. A1: adder port map (v,w,x,y,z) 

  
v,w, and y must be bit_vectors, y and z bits 
  
(2) "Named association": each signal-to-port connection is listed explicitly
as "signal=&gt;port". 

 
Example 
A1: adder port map(a=&gt;v, b=&gt;w, s=&gt;y, cin-&gt;x, cout-&gt;z);
 (The signal ordering is not important in this format)
  
Example: 

     architecture r1 of register is
       component jkff
          port(J,K,CLK: in bit;
               Q,QN:    out bit);
       end component;
       for ALL: jkff use entity work.jkff (equations);
       -- Use architecture  equations  of entity jkff
          for all instances

       component dff
          port(D,CLK: in bit;
               Q,QN:  out bit);
       end component; 
       for DFF1: dff  use entity work.dff  (equations);
       for DFF2: dff  use entity work.dff  (circuit);
       --Use different architectures of  dff  for instances
         DFF1 and DFF2
    begin
       JKFF1: jkff port map (j1,k1,clk,q1,qn1);
       JKFF2: jkff port map (j2,k1,clk,q2,qn2);
       DFF1:  dff  port map (d1,clk,q4,qn4);
       DFF2:  dff  port map (d2,clk,q5,qn5);
    end. 

<h3>
  Concurrent assertion -  Back To Top </h3>
A  concurrent assertion statement  checks a condition (occurrence
of an event) and issues a report if the condition is not true. This can
be used to check for timing violations, illegal conditions, etc. An optional
severity level can be reported to indicate the nature of the detected condition.
 
Syntax: 

     <b>assert</b>  (clear /= '1') or (preset /= '1')
    <b>report</b> "Both preset and clear are set!"
    <b>severity warning;</b> 

<h3>
  Generate statement -  Back To Top </h3>
A  generate statement  is an iterative or conditional elaboration
of a portion of a description. This provides a compact way to represent
what would ordinarily be a group of statements.
 
Example 
Generate a 4-bit full adder from 1-bit full_adder stages:
   add_label:        -- Note that a label is required here
    for i in 4 downto 1 generate
        FA: full_adder port map(C(i-1), A(i), B(i), C(i), Sum(i));
    end generate; 

 The resulting code would look like:

    FA4: full_adder port map(C(3), A(4), B(4), C(4), Sum(4));
    FA3: full_adder port map(C(2), A(3), B(3), C(3), Sum(3));
    FA2: full_adder port map(C(1), A(2), B(2), C(2), Sum(2));
    FA1: full_adder port map(C(0), A(1), B(1), C(1), Sum(1)); 

<h2>
  SEQUENTIAL STATEMENTS -  Back To
Top </h2>
Sequential statements are used to define algorithms to express the behavior
of a design entity. These statements appear in process statements and in
subprograms (procedures and functions).
<h3>
  Wait statement -  Back To Top </h3>
- suspends process/subprogram execution until a signal changes, a condition
becomes true, or a defined time period has elapsed. Combinations of these
can also be used.
 
Syntax: 

     <b>wait</b> [<b>on</b> signal_name {,signal_name}]
         [<b>until</b> condition]
         [<b>for</b> time expression] 

 
Example 
Suspend execution until one of the two conditions becomes true, or for
25ns, whichever occurs first.
     wait until clock = '1' or enable /='1' for 25ns; 

<h3>
  Signal assignment statement -  Back
To Top </h3>
Assign a waveform to one signal driver (edit the event queue).
 
Example 

     A &lt;= B after 10ns;
    C &lt;= A after 10ns;  -- value of C is current A value 

<h3>
  Variable assignment statement -  Back
To Top </h3>
Update a process/procedure/function variable with an expression. The update
takes affect immediately.
 
Example 

     A := B and C;
    D := A;        -- value of D is new A value 

<h3>
  Procedure call -  Back To Top </h3>
Invoke an externally-defined subprogram in the same manner as a concurrent
procedure call.
<h3>
  Conditional Statements -  Back
To Top </h3>
Standard  if..then  and  case  constructs can be used for selective
operations.
     <b>if</b> condition <b>then
</b>       ... sequence of statements... 
    <b>elsif</b> condition <b>then
</b>       ... sequence of statements...
    <b>else</b> 
       ... sequence of statements...
    <b>end if;</b> 
NOTE:  elsif  and  else  clauses are optional.
     <b>case</b> expression <b>is
</b>       <b>when</b> choices <b>=&gt;</b> sequence of statements
       <b>when</b> choices <b>=&gt;</b> sequence of statements
       ...
       <b>when others</b> <b>=&gt;</b> sequence of statements
    <b>end case;</b> 
NOTE:  case  choices can be expressions or ranges.
<h3>
  Loop statements -  Back To Top </h3>
Sequences of statements can be repeated some number of times under the
control of <b>while</b> or <b>for</b> constructs.
  label: <b>while</b> condition <b>loop
</b>    ... sequence of statements ...
    <b>end loop</b> label; 

  label:  <b>for</b> loop_variable <b>in</b> range <b>loop
</b>  ... sequence of statements...
   <b>end loop</b> label; 
NOTE: the label is optional.
 <b>Loop termination statements</b> - allow termination of one iteration,
loop, or procedure.
  <b>next</b> <b>[when</b> condition<b>];</b> -- end current loop iteration
  <b>exit [when</b> condition<b>];</b> -- exit innermost loop entirely
  <b>return</b> expression; -- exit from subprogram
  NOTES: 1. The next/exit condition clause is optional.
  2. The return expression is used for functions.
  
8. <b>Sequential assertion</b> - same format as a concurrent assertion. 

<h3>
  PROCEDURES -  Back To Top </h3>
A  procedure  is a subprogram that is passed parameters and may return
values via a parameter list.
 
Example 

    <b>procedure</b>  proc_name (signal clk: in vlbit;
                         constant d: in vlbit;
                         signal data: out vlbit) <b>is
</b>       ... local variable declarations ...
   <b>begin
</b>       ... sequence of statements ...
   <b>end</b> proc_name; 
Procedure call: proc_name(clk1, d1, dout);
<h3>
  FUNCTIONS -  Back To Top </h3>
A  function  is a subprogram that is passed parameters and returns
a single value. Unlike procedures, functions are primarily used in expressions.
 
Example 

    -- Convert bit_vector to IEEE std_logic_vector format
   -- (attributes LENGTH and RANGE are described below)
   <b>function</b> bv2slv (b:bit_vector) <b>return</b> std_logic_vector <b>is
</b>       variable result: std_logic_vector(b'LENGTH-1 downto 0);
   <b>begin
</b>       for i in result'RANGE loop
           case b(i) is
               when '0' =&gt; result(i) := '0';
               when '1' =&gt; result(i) := '1';
           end case;
       end loop;
       return result;
   <b>end;</b> 

    -- Convert bit_vector to unsigned (natural) value 
   <b>function</b> b2n (B: bit_vector) <b>return</b> Natural <b>is
</b>       variable S: bit_vector(B'Length - 1 downto 0) := B;
       variable N: Natural := 0;
   <b>begin
</b>       for i in S'Right to S'Left loop
           if S(i) = '1' then
               N := N + (2**i);
           end if;
       end loop;
       return N;
   <b>end;</b> 

 
Function Calls: 

        signal databus:  vector4(15 downto 0);
       signal internal: bit_vector (15 downto 0);
       variable x: integer;
        ....
       databus &lt;= bv2slv (internal);
       x := b2n(internal); 
Data conversion between ieee types and bit/bit_vector (functions in "ieee.std_logic_1164")
 <b>To_bit(sul)</b> - from std_ulogic to bit
 <b>To_bitvector(sulv)</b> - from std_ulogic_vector/std_logic_vector
 <b>To_StdULogic(b)</b> - from bit to std_ulogic
 <b>To_StdLogicVector(bv)</b> - from bit_vector or std_ulogic_vector
 <b>To_StdULogicVector(bv)</b>- from bit_vector or std_logic_vector
 <b>To_X01(v)</b> - from bit, std_ulogic, or std_logic to X01
 <b>To_X01Z(v)</b> - from bit, std_ulogic, or std_logic to X01Z
 <b>To_UX01(v)</b> - from bit, std_ulogic, or std_logic to UX01 <h3>
  Other "ieee.std_logic_1164" functions -  Back
To Top </h3>

 <b>rising_edge(s)</b> - true if rising edge on signal s (std_ulogic)
 <b>falling_edge(s)</b> - true if falling edge on signal s (std_ulogic) 
Additional Mentor Graphics-supplied functions for elements of types Bit_vector
(implemented as overloaded operator definitions):
 <b>library mgc_portable;</b>
 <b>use mgc_portable.qsim_logic.ALL;</b> 
Arithmetic between bit_vectors: use normal binary operator tokens
 a + b, a - b, a * b, a / b, a mod b, a rem b
  Logical operations between all signal types and vectors of signal types
in the "ieee" library.
  <b>and, or, nand, nor, xor, xnor, not</b>
  Shift/rotate left/right logical/arithmetic operators:
  <b>sll, srl, sra, rll, rrl</b>
  Ex. a := x sll 2; -- "shift left logical" bit_vector x by 2 bits
  Relational operations: <b>=,/=,&lt;,&gt;,&lt;=,&gt;=</b>
  Type conversion:
  <b>to_bit</b> (from integer)
 <b>to_integer</b> (from bit_vector) <h2>
  OBJECT ATTRIBUTES -  Back To Top </h2>
An  object attribute  returns information about a signal or data type.
<h3>
Signal Condition Attributes (for a signal S)</h3>

 <b>S'DELAYED(T)</b> - value of S delayed by T time units
 <b>S'STABLE(T)</b> - true if no event on S over last T time units
 <b>S'QUIET(T)</b> - true if S quiet for T time units
 <b>S'LAST_VALUE</b> - value of S prior to latest change
 <b>S'LAST_EVENT</b> - time at which S last changed
 <b>S'LAST_ACTIVE</b> - time at which S last active
 <b>S'EVENT</b> - true if an event has occurred on S in current cycle
 <b>S'ACTIVE</b> - true if signal S is active in the current cycle
 <b>S'TRANSACTION</b> - bit value which toggles each time signal S changes  
Examples 

     if (clock'STABLE(0ns)) then  -- change in clock?
        ...      -- action if no clock edge
    else
        ...      -- action on edge of clock
    end if; 

     if clock'EVENT and clock = '1' then
        Q &lt;= D after 5ns;       -- set Q to D on rising edge of clock
    end if; 

<h3>
Data Type Bounds (Attributes of data type T)</h3>

 <b>T'BASE</b> - base type of T
 <b>T'LEFT</b> - left bound of data type T
 <b>T'RIGHT</b> - right bound
 <b>T'HIGH</b> - upper bound (may differ from left bound)
 <b>T'LOW</b> - lower bound <h3>
Enumeration Data Types (Variable/signal x of data type T)</h3>

 <b>T'POS(x)</b> - position number of value of x of type T
 <b>T'VAL(x)</b> - value of type T whose position number is x
 <b>T'SUCC(x)</b> - value of type T whose position is x+1
 <b>T'PRED(x)</b> - value of type T whose position is x-1
 <b>T'LEFTOF(x)</b> - value of type T whose position is left of x
 <b>T'RIGHTOF(x)</b> - value of type T whose position is right of x <h3>
Array Indexes for an Array A (Nth index of array A)</h3>

 <b>A'LEFT(N)</b> - left bound of index
 <b>A'RIGHT(N)</b> - right bound of index
 <b>A'HIGH(N)</b> - upper bound of index
 <b>A'LOW(N)</b> - lower bound of index
 <b>A'LENGTH(N)</b> - number of values in range of index
 <b>A'RANGE(N)</b> - range: A'LEFT to A'RIGHT
 <b>A'REVERSE_RANGE(N)</b> - range A'LEFT downto A'RIGHT 
NOTE: For multi-dimensional array, Nth index must be indicated in the attribute
specifier. N may be omitted for a one-dimensional array.
 
Examples 

     for i in (data_bus'RANGE) loop
        ...
    for i in (d'LEFT(1) to d'RIGHT(1)) loop
        ... 

<h3>
Block Attributes (of a block B)</h3>

 <b>B'BEHAVIOR</b> - true if block B contains no component instantiations
 <b>B'STRUCTURE</b> - true if no signal assignment statements in block
B <h2>
  THE TEXTIO PACKAGE -  Back To Top </h2>
 TEXTIO  is a package of VHDL functions that read and write text files.
To make the package visible:
 <b>use std.textio.all;</b> <h3>
Data Types:</h3>

 <b>text</b> - a file of character strings
 <b>line</b> - one string from a text file  
Example Declarations 

    <b>file</b> Prog: <b>text is in</b> "file_name"; --text file "file_name"
   <b>variable</b> L: <b>line</b>;              -- read lines from file to L 

<h3>
Reading Values From a File:</h3>

  
<b>readline(F, L)</b> 

 
Read one line from "text" file F to "line" L 

  
 
<b>read(L, VALUE, GOOD);</b> 

 
Read one value from "line" L into variable VALUE 

  
GOOD is TRUE if successful 

 
Data_type of VALUE can be bit, bit_vector, integer, real, character, string,
or time. 
  <h3>
Writing values to a file:</h3>

  
<b>writeline(F, L);</b> 

 
Write one line to "text" file F from "line" L 

  
 
<b>write(L, VALUE, JUSTIFY, FIELD);</b> 

 
Write one value to "line" L from variable VALUE 

  
Data_type of VALUE can be bit, bit_vector, integer, real, character, string,
or time. 

 
JUSTIFY is "left" or "right" to justify within the field 

 
FIELD is the desired field width of the written value 

  
     
  Back To Top 
</body> 